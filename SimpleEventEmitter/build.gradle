apply plugin: 'java'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'org.assertj:assertj-core:2.2.0'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def packageName = 'info.izumin.java'
def artifactName = 'simpleeventemitter'
def versionName = '1.1.0'
def siteUrl = 'https://github.com/izumin5210/SimpleEventEmitter'
def gitUrl = 'https://github.com/izumin5210/SimpleEventEmitter.git'

group = packageName
version = versionName

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Simple EventEmitter implementation like node.js'
        websiteUrl = siteUrl
        issueTrackerUrl = "${siteUrl}/issues"
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['EventEmitter']
        publicDownloadNumbers = true
        version {
            name = versionName
            desc = "SimpleEventEmitter ${versionName}"
            released  = new Date()
            vcsTag = versionName
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId packageName
            artifactId artifactName
            version versionName

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}
